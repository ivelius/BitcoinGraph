apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.example.yanbraslavski.bitcoingraph"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //This endpoint declaration can be different in case there are
        //several endpoints (for example "staging" or "production")
        buildConfigField "String", "ENDPOINT", "\"https://blockchain.info/\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def supportLibraryVersion = "24.1.1"
def espressoVersion = "2.2.2"
def retrofitVersion = "2.1.0"
def rxJavaVersion = "1.1.6"
def rxAndroidVersion = "1.2.1"
def annimonStreamApiVersion = "1.1.1"
def androidTestRunnerVersion = "0.4.+"
def junitVersion = "4.12"
def mockitoVersion = '1.10.19'
def powerMockito = '1.6.2'
def daggerVersion = "2.5"
def butterKnifeVersion = "8.2.1"

//this is a not so elegant fix for conflicting dependencies
configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // dependancy injections
    // apt command comes from the android-apt plugin
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0'
    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    //stream api
    compile "com.annimon:stream:$annimonStreamApiVersion"

    //support library
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"

    //Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    //Rx Java
    compile "io.reactivex:rxandroid:$rxAndroidVersion"
    compile "io.reactivex:rxjava:$rxJavaVersion"

    //Instrumentation Tests
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test:runner:$androidTestRunnerVersion"
    androidTestCompile "com.android.support.test:rules:$androidTestRunnerVersion"

    //Unit tests
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-all:$mockitoVersion"
    testCompile "org.powermock:powermock-module-junit4:$powerMockito"
    testCompile "org.powermock:powermock-api-mockito:$powerMockito"
}
